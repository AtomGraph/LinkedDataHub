PREFIX dh:    <https://www.w3.org/ns/ldt/document-hierarchy#>
PREFIX sioc:  <http://rdfs.org/sioc/ns#>
PREFIX foaf:  <http://xmlns.com/foaf/0.1/>
PREFIX skos:  <http://www.w3.org/2004/02/skos/core#>
PREFIX dct:   <http://purl.org/dc/terms/>

CONSTRUCT
{
    GRAPH ?item
    {
        ?concept ?p ?o .
        ?item a dh:Item ;
            foaf:primaryTopic ?concept ;
            sioc:has_container <concepts/> ;
            dh:slug ?id ;
            dct:title ?prefLabel .
    }
}
WHERE
{
    SELECT *
    {
        ?concept a skos:Concept .
        BIND (STRAFTER(STR(?concept), "http://vocabularies.unesco.org/thesaurus/") AS ?id)
        BIND (uri(concat(str(<concepts/>), encode_for_uri(?id), "/")) AS ?item)

        ?concept ?p ?o
        OPTIONAL
        {
            ?concept skos:prefLabel ?prefLabel
            FILTER (langMatches(lang(?prefLabel), "en"))
        }
    }
}