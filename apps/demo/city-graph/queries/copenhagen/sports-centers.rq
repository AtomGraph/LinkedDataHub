PREFIX foaf:       <http://xmlns.com/foaf/0.1/>
PREFIX dct:        <http://purl.org/dc/terms/>
PREFIX dbo:        <http://dbpedia.org/ontology/> 
PREFIX schema:     <https://schema.org/> 
PREFIX geo:        <http://www.w3.org/2003/01/geo/wgs84_pos#> 
PREFIX xsd:        <http://www.w3.org/2001/XMLSchema#> 
PREFIX dh:         <https://www.w3.org/ns/ldt/document-hierarchy/domain#>

CONSTRUCT
{
  ?item a ?itemType ;
       dh:slug ?id ;
       dct:title ?name ;
       foaf:primaryTopic ?sportCenter .
  ?sportCenter a ?type ;
     foaf:isPrimaryTopicOf ?item ;
     geo:lat ?lat ;
     geo:long ?long ;
     dct:title ?name ;
     schema:streetAddress ?address ;
     schema:postalCode ?postcode ;
     foaf:mbox ?email ;
     foaf:page ?link ;
     dct:identifier ?id .
}
WHERE
{
  BIND(<ns/default#Item> AS ?itemType)
  BIND(<ns/domain#SportsCenter> AS ?type)
  BIND (BNODE() AS ?item)
  
  ?sportCenter <#FID> ?id ;
              	<#name> ?name ;
              	<#address> ?address ;
              	<#lat> ?lat_string ;
              	<#postcode> ?postcode ;
              	<#long> ?long_string .

  BIND(xsd:float(?lat_string) AS ?lat)
  BIND(xsd:float(?long_string) AS ?long)

	OPTIONAL {
       ?sportCenter <#email> ?emailString
       BIND (URI(CONCAT(STR(<mailto:>), ?emailString)) AS ?email)
    }
    OPTIONAL {
       ?sportCenter <#link> ?linkString
		   BIND (URI(?linkString) AS ?link) 
    }
}