PREFIX foaf:       <http://xmlns.com/foaf/0.1/>
PREFIX dct:        <http://purl.org/dc/terms/>
PREFIX dbo:        <http://dbpedia.org/ontology/> 
PREFIX schema:     <http://schema.org/> 
PREFIX geo:        <http://www.w3.org/2003/01/geo/wgs84_pos#> 
PREFIX xsd:        <http://www.w3.org/2001/XMLSchema#> 
PREFIX dh:         <https://www.w3.org/ns/ldt/document-hierarchy/domain#>

CONSTRUCT
{
  ?item a ?itemType ;
       dh:slug ?id ;
       dct:title ?name ;
       foaf:primaryTopic ?toilet .
  ?toilet a ?type ;
          foaf:isPrimaryTopicOf ?item ;
          geo:lat ?lat ;
          geo:long ?long ;
          schema:streetAddress ?address ;
          schema:postalCode ?postcode ;
          schema:additionalProperty ?toiletType ;
          dbo:isHandicappedAccessible ?accessible ;
          schema:location ?location ;
          schema:comment ?comment ;
          schema:openingHours ?openingHours ;
          dct:identifier ?id .
}
WHERE
{
  BIND(uri(concat(str(?base), "ns/default#Item")) AS ?itemType)
  BIND(uri(concat(str(?base), "ns#Toilet")) AS ?type)
  BIND (BNODE() AS ?item)
  
  ?toilet <#FID> ?id ;
          <#title> ?name ;
          <#adresse> ?address ;
          <#latitude> ?lat_string ;
          <#longitude> ?long_string .

  BIND(xsd:float(?lat_string) AS ?lat)
  BIND(xsd:float(?long_string) AS ?long)

  OPTIONAL { ?toilet <#postnr> ?postcode }
  OPTIONAL { ?toilet <#toilet_type> ?toiletType }
  OPTIONAL { ?toilet <#handicapadgang> ?accessible }
  OPTIONAL { ?toilet <#placering> ?location }
  OPTIONAL { ?toilet <#aabningstid_bemrk> ?comment }
  OPTIONAL { ?toilet <#opening_hours> ?openingHours }
}