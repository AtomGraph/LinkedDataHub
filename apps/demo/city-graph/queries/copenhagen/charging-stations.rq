PREFIX foaf:       <http://xmlns.com/foaf/0.1/>
PREFIX dct:        <http://purl.org/dc/terms/>
PREFIX schema:     <https://schema.org/> 
PREFIX geo:        <http://www.w3.org/2003/01/geo/wgs84_pos#> 
PREFIX xsd:        <http://www.w3.org/2001/XMLSchema#> 
PREFIX rdf:        <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dh:         <https://www.w3.org/ns/ldt/document-hierarchy/domain#>
PREFIX seas:	   <https://w3id.org/seas/>

CONSTRUCT
{
  ?item a ?itemType ;
       dh:slug ?id ;
       dct:title ?name ;
       foaf:primaryTopic ?charger .
  ?charger a ?type ;
	   foaf:isPrimaryTopicOf ?item ;
	   geo:lat ?lat ;
	   geo:long ?long ;
	   schema:streetAddress ?address ;
	   dct:title ?name ;
	   schema:additionalProperty ?chargeType ;
	   schema:maximumAttendeeCapacity ?spaces ;
	   schema:serviceOperator ?operator ;
	   schema:comment ?information ;
	   dct:identifier ?id .
}
WHERE
{
  BIND(uri(concat(str(?base), "ns/default#Item")) AS ?itemType)
  BIND(uri(concat(str(?base), "ns/domain#ChartingStation")) AS ?type)
  BIND (BNODE() AS ?item)
  
  ?charger <#FID> ?id ;
               <#lat> ?lat_string ;
               <#long> ?long_string . 

  BIND(xsd:float(?lat_string) AS ?lat)
  BIND(xsd:float(?long_string) AS ?long)

  OPTIONAL 
  { ?charger <#name> ?name }
  OPTIONAL 
  { ?charger <#chargeType> ?chargeType }
  OPTIONAL 
  { ?charger <#address> ?address }
  OPTIONAL 
  { ?charger <#antal_udtag> ?spaces }
  OPTIONAL 
  { ?charger <#operator> ?operator }
  OPTIONAL 
  { ?charger <#bem> ?information }
}