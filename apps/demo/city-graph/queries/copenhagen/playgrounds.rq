PREFIX dh:         <https://www.w3.org/ns/ldt/document-hierarchy/domain#>
PREFIX foaf:       <http://xmlns.com/foaf/0.1/>
PREFIX dct:        <http://purl.org/dc/terms/>
PREFIX schema:     <http://schema.org/> 
PREFIX geo:        <http://www.w3.org/2003/01/geo/wgs84_pos#> 
PREFIX xsd:        <http://www.w3.org/2001/XMLSchema#> 
PREFIX rdf:        <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

CONSTRUCT
{
  ?item a ?itemType ;
       dct:title ?name ;
       dh:slug ?id ;
       foaf:primaryTopic ?playground .
  ?playground a ?type ;
    foaf:isPrimaryTopicOf ?item ;
    geo:lat ?lat ;
  	geo:long ?long ;
  	dct:identifier ?id ;
  	dct:title ?name ;
  	schema:streetAddress ?address_description;
  	foaf:based_near [ dct:title ?area ] ;
  	rdf:type [ dct:title ?playground_type ] ;
  	schema:typicalAgeRange ?age_group ;
  	dct:description  ?description ;
  	foaf:page ?link .
}
WHERE
{
  BIND(uri(concat(str(?base), "ns/default#Item")) AS ?itemType)
  BIND (uri(concat(str(?base), 'ns#Playground')) AS ?type)
  BIND (BNODE() as ?item)
  
  ?playground <#FID> ?id ;
	            <#name> ?name ;
	            <#address_description> ?address_description ;
              <#lat> ?lat_string ;
	            <#long> ?long_string  .

  BIND (xsd:float(?lat_string) AS ?lat) .
  BIND (xsd:float(?long_string) AS ?long) .

  OPTIONAL 
    {?playground <#area> ?area}
	OPTIONAL 
    {?playground <#playground_type> ?playground_type}
	OPTIONAL 
    {?playground <#age_group> ?age_group}
	OPTIONAL 
    {?playground <#description> ?description}
	OPTIONAL 
    {?playground <#link> ?linkString 
     BIND (URI(?linkString) AS ?link)}
}