PREFIX ldt:     <https://www.w3.org/ns/ldt#>
PREFIX sd:      <http://www.w3.org/ns/sparql-service-description#>
PREFIX a:       <https://w3id.org/atomgraph/core#>
PREFIX lapp:    <https://w3id.org/atomgraph/linkeddatahub/apps#>
PREFIX foaf:    <http://xmlns.com/foaf/0.1/>

SELECT ?endUserApp ?endUserBase ?endUserQuadStore ?endUserEndpoint ?endUserAuthUser ?endUserAuthPwd ?endUserMaker ?adminApp ?adminBase ?adminQuadStore ?adminEndpoint ?adminAuthUser ?adminAuthPwd ?adminMaker
{
    ?endUserApp ldt:base ?endUserBase ;
        ldt:service ?endUserService ;
        lapp:adminApplication ?adminApp .
        ?adminApp ldt:service ?adminService ;
            ldt:base ?adminBase .
    ?endUserService a:quadStore ?endUserQuadStore ;
        sd:endpoint ?endUserEndpoint .
    ?adminService a:quadStore ?adminQuadStore ;
        sd:endpoint ?adminEndpoint .
    OPTIONAL
    {
        ?endUserService a:authUser ?endUserAuthUser ;
            a:authPwd ?endUserAuthPwd .
    }
    OPTIONAL
    {
        ?adminService a:authUser ?adminAuthUser ;
            a:authPwd ?adminAuthPwd .
    }
    OPTIONAL
    {
        ?endUserService foaf:maker ?endUserMaker
    }
    OPTIONAL
    {
        ?adminService foaf:maker ?adminMaker
    }
}