PREFIX apl:     <https://w3id.org/atomgraph/linkeddatahub/domain#>
PREFIX acl:     <http://www.w3.org/ns/auth/acl#>
PREFIX dh:      <https://www.w3.org/ns/ldt/document-hierarchy/domain#>
PREFIX prov:    <http://www.w3.org/ns/prov#>
PREFIX void:    <http://rdfs.org/ns/void#>
PREFIX foaf:    <http://xmlns.com/foaf/0.1/>
PREFIX sioc:    <http://rdfs.org/sioc/ns#>
PREFIX dct:     <http://purl.org/dc/terms/>

CONSTRUCT
{
    GRAPH ?graphGraphDoc {
        ?graphDoc a dh:Item ;
            sioc:has_space <> ;
            sioc:has_container <graphs/> ;
            foaf:maker <${OWNER_URI}> ;
            acl:owner <${OWNER_URI}> ;
            foaf:primaryTopic ?graph ;
            prov:generatedAtTime ?now .
        ?graph a apl:Dataset ;
            foaf:isPrimaryTopicOf ?graphDoc .
        ?doc sioc:has_space <> ;
            foaf:maker <${OWNER_URI}> ;
            acl:owner <${OWNER_URI}> ;
            dct:created ?now ;
            void:inDataset ?graph .
    }
    GRAPH ?graphDoc {
        ?s ?p ?o
    }
}
WHERE
{
    {
        SELECT DISTINCT ?s ?doc 
        {
            ?s ?p ?o .
            BIND (URI(IF (CONTAINS(STR(?s), "#"), STRBEFORE(STR(?s), "#"), ?s)) AS ?doc)
        }
    }
    BIND(SHA1(STR(?doc)) AS ?docSha1)
    BIND (URI(CONCAT(STR(<graphs/>), ?docSha1, "/")) AS ?graphDoc)
    BIND (URI(CONCAT(STR(?graphDoc), "#this")) AS ?graph)
    BIND(SHA1(STR(?graph)) AS ?graphSha1)
    BIND (URI(CONCAT(STR(<graphs/>), ?graphSha1, "/")) AS ?graphGraphDoc)
    BIND (NOW() AS ?now)
    ?s ?p ?o .
}