PREFIX ldt:     <https://www.w3.org/ns/ldt#>
PREFIX sd:      <http://www.w3.org/ns/sparql-service-description#>
PREFIX lapp:    <https://w3id.org/atomgraph/linkeddatahub/apps/domain#>
PREFIX dydra:   <http://dydra.com/ns#>
PREFIX srv:     <http://jena.hpl.hp.com/Service#>

SELECT ?endUserQuadStore ?endUserAuthUser ?endUserAuthPwd ?adminBaseUri ?adminQuadStore ?adminAuthUser ?adminAuthPwd
{
    ?app ldt:base <$BASE_URI> ;
        ldt:service ?endUserService ;
        lapp:adminApplication ?adminApp .
        ?adminApp ldt:service ?adminService ;
            ldt:base ?adminBaseUri .
        {
            ?endUserService sd:endpoint ?endUserQuadStore .
        }
        UNION
        {
            ?endUserService dydra:repository ?endUserRepository .
            BIND (URI(CONCAT(STR(?endUserRepository), "service")) AS ?endUserQuadStore)
        }
        {
            ?adminService sd:endpoint ?adminQuadStore .
        }
        UNION
        {
            ?adminService dydra:repository ?adminRepository .
            BIND (URI(CONCAT(STR(?adminRepository), "service")) AS ?adminQuadStore)
        }
        OPTIONAL
        {
            ?endUserService srv:queryAuthUser ?endUserAuthUser ;
                srv:queryAuthPwd ?endUserAuthPwd .
        }
        OPTIONAL
        {
            ?adminService srv:queryAuthUser ?adminAuthUser ;
                srv:queryAuthPwd ?adminAuthPwd .
        }
}