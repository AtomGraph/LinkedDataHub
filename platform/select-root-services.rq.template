PREFIX ldt:     <https://www.w3.org/ns/ldt#>
PREFIX sd:      <http://www.w3.org/ns/sparql-service-description#>
PREFIX a:       <https://w3id.org/atomgraph/core#>
PREFIX lapp:    <https://w3id.org/atomgraph/linkeddatahub/apps/domain#>
PREFIX dydra:   <https://w3id.org/atomgraph/linkeddatahub/services/dydra#>

SELECT ?endUserQuadStore ?endUserAuthUser ?endUserAuthPwd ?adminBaseUri ?adminQuadStore ?adminAuthUser ?adminAuthPwd
{
    ?app ldt:base <$BASE_URI> ;
        ldt:service ?endUserService ;
        lapp:adminApplication ?adminApp .
        ?adminApp ldt:service ?adminService ;
            ldt:base ?adminBaseUri .
        {
            ?endUserService a:quadStore ?endUserQuadStore .
        }
        UNION
        {
            ?endUserService dydra:repository ?endUserRepository .
            BIND (URI(CONCAT(STR(?endUserRepository), "service")) AS ?endUserQuadStore)
        }
        {
            ?adminService a:quadStore ?adminQuadStore .
        }
        UNION
        {
            ?adminService dydra:repository ?adminRepository .
            BIND (URI(CONCAT(STR(?adminRepository), "service")) AS ?adminQuadStore)
        }
        OPTIONAL
        {
            ?endUserService a:authUser ?endUserAuthUser ;
                a:authPwd ?endUserAuthPwd .
        }
        OPTIONAL
        {
            ?adminService a:authUser ?adminAuthUser ;
                a:authPwd ?adminAuthPwd .
        }
}